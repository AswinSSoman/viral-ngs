from os.path import join

samples = list(read_samples_file(config.get("samples_metagenomics")))

rule all_metagenomics:
    input:
        expand(join(config["data_dir"], config["subdirs"]["metagenomics"],
               "{sample}.kraken.report"), sample=samples),
        # expand(join(config["data_dir"], config["subdirs"]["metagenomics"],
        #             "{sample}.diamond.m8.gz"), sample=samples),
        expand(join(config["data_dir"], config["subdirs"]["metagenomics"],
                    "{sample}.{method}.krona.html"), sample=samples, method=['kraken', 'diamond'])
    params: LSF='-N'

rule diamond:
    input: join(config["data_dir"], config["subdirs"]["source"], "{sample}.bam")
    output: report=join(config["data_dir"], config["subdirs"]["metagenomics"], "{sample}.diamond.report"),
            lca=join(config["data_dir"], config["subdirs"]["metagenomics"], "{sample}.diamond.lca.gz")
    resources: cores=int(config.get("number_of_threads", 1))
    params: numThreads=str(config.get("number_of_threads", 1))
    shell:
        """
        {config[bin_dir]}/metagenomics.py diamond {input} {config[diamond_db]} {config[taxonomy_db]} {output.report} --outLca {output.lca} --numThreads {params.numThreads}
        """

rule kraken:
    input: join(config["data_dir"], config["subdirs"]["source"], "{sample}.bam")
    output: report=join(config["data_dir"], config["subdirs"]["metagenomics"], "{sample}.kraken.report"),
            reads=join(config["data_dir"], config["subdirs"]["metagenomics"], "{sample}.kraken.reads.gz")
    resources: cores=int(config.get("number_of_threads", 1))
    params: numThreads=str(config.get("number_of_threads", 1))
    shell:
        """
        {config[bin_dir]}/metagenomics.py kraken {input} {config[kraken_db]} --outReads {output.reads} --outReport {output.report} --numThreads {params.numThreads}
        """

method_reads_ext = {
    'diamond': 'diamond.lca.gz',
    'kraken': 'kraken.reads.gz'
}

method_krona_columns = {
    'diamond':  (1, 2),
    'kraken':  (2, 3)
}

rule krona_import_taxonomy:
    input: lambda wildcards: join(config["data_dir"], config["subdirs"]["metagenomics"], "{sample}.%s" % method_reads_ext[wildcards.method])
    output: join(config["data_dir"], config["subdirs"]["metagenomics"], "{sample,\w+}.{method,\w+}.krona.html")
    run:
        query_col, taxid_col = method_krona_columns[method]
        shell("""
        {config[bin_dir]}/metagenomics.py krona {input} {config[krona_db]} {output} --noRank \
          --queryColumn {query_col}  --taxidColumn {taxid_col}
        """)
